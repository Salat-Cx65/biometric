apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    //compileSdkPreview compileSDK
    compileSdkVersion compileSDK
    defaultConfig {
        //vector drawables + scaleType="centerCrop" is broken on Android 5.0 - 5.1
        vectorDrawables.useSupportLibrary = true
        minSdkVersion minSDK
        //targetSdkPreview targetSDK
        targetSdkVersion targetSDK
        consumerProguardFiles "proguard-project-biometric.pro"
    }
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    kotlinOptions {
        jvmTarget = javaVersion.toString()
    }
    namespace 'dev.skomlach.biometric.compat'
}

dependencies {
    implementation project(":" + libArtifactId_soter)//2.1.8
    implementation project(":" + libArtifactId_common)
    compileOnly project(":" + libArtifactId_biometric_api)
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'
    implementation 'com.jaredrummler:android-device-names:2.1.0'
    implementation "org.jsoup:jsoup:1.15.3"

    implementation "androidx.biometric:biometric-ktx:1.2.0-alpha05"

    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'com.google.guava:guava:31.1-android'
    implementation "androidx.palette:palette-ktx:1.0.0"
}

afterEvaluate {
    android.libraryVariants.all { v ->
        if (v.buildType.name == "release") {
            v.assemble.doFirst {
                def f = new File("$projectDir/src/main/assets/by_brand.json")
                println("Path" + f.absolutePath)
                if (f.exists()) {
                    f.delete()
                }
                new URL("https://github.com/androidtrackers/certified-android-devices/blob/master/by_brand.json?raw=true")
                        .withInputStream { i -> f.withOutputStream { it << i } }

            }
        }
    }
}
// Publishing
ext {
    PUBLISH_GROUP_ID = libsGroupId
    PUBLISH_ARTIFACT_ID = libArtifactId_biometric
    PUBLISH_VERSION = libVersion
}

apply from: "${rootProject.projectDir}/scripts/publish-mavencentral.gradle"